plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
}
repositories {
    mavenCentral()
}

group = "io.github.sonots"
version = "0.2.0"
description = "Embulk filter plugin to print embulk records to stdout"

embulkPlugin {
    mainClass = "org.embulk.filter.stdout.StdoutFilterPlugin"
    category = "filter"
    type = "stdout"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.32"
    compileOnly "org.embulk:embulk-spi:0.10.32"

    compile("org.embulk:embulk-util-config:0.3.1") {
        // Jackson libraries conflict with embulk-core before v0.10.32.
        // They are once excluded here, and re-added explicitly below.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }
    compile "org.embulk:embulk-util-timestamp:0.2.1"

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.19.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    //compile "joda-time:joda-time:2.9.2"

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testCompile "junit:junit:4.13.2"
}

// This Gradle plugin's POM dependency modification works for "maven-publish" tasks.
//
// Note that "uploadArchives" is no longer supported. It is deprecated in Gradle 6 to be removed in Gradle 7.
// https://github.com/gradle/gradle/issues/3003#issuecomment-495025844
publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

gem {
    from("LICENSE.txt")
    from("NOTICE")
    authors = [ "Naotoshi Seo" ]
    email = [ "sonots@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Stdout filter plugin for Embulk"
    homepage = "https://github.com/sonots/embulk-filter-stdout"
    licenses = [ "MIT" ]
    //metadata = []
}

gemPush {
    host = "https://rubygems.org"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}
